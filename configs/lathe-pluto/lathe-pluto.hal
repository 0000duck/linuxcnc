# load realtime modules
loadrt trivkins
loadrt [EMCMOT]EMCMOT servo_period_nsec=[EMCMOT]SERVO_PERIOD traj_period_nsec=[EMCMOT]TRAJ_PERIOD key=[EMCMOT]SHMEM_KEY num_joints=[TRAJ]AXES
loadrt at_pid num_chan=2 debug=1
loadrt ddt count=4 
loadrt pluto_servo

# define the order of execution for RT code
addf pluto-servo.read servo-thread
addf pluto-servo.write servo-thread

addf motion-command-handler    servo-thread
addf motion-controller         servo-thread
addf pid.0.do-pid-calcs        servo-thread
addf pid.1.do-pid-calcs        servo-thread
addf ddt.0                     servo-thread
addf ddt.1                     servo-thread
addf ddt.2                     servo-thread
addf ddt.3                     servo-thread

# hook stuff together
newsig Xpos-fb float
newsig Zpos-fb float

newsig spindle-index-enable bit
linkps motion.spindle-index-enable <=> spindle-index-enable
linksp spindle-index-enable <=> pluto-servo.encoder.2.index-enable

newsig spindle-pos float
linkps pluto-servo.encoder.2.position => spindle-pos
linksp spindle-pos => motion.spindle-revs

setp pluto-servo.encoder-0-scale [AXIS_0]INPUT_SCALE
setp pluto-servo.encoder-1-scale [AXIS_2]INPUT_SCALE
setp pluto-servo.encoder-2-scale -4096

linksp Xpos-fb <= pluto-servo.encoder.0.position
linksp Zpos-fb <= pluto-servo.encoder.1.position

newsig Xvel-cmd float
newsig Zvel-cmd float

linksp Xpos-fb => pid.0.feedback
linksp Zpos-fb => pid.1.feedback

linksp Xpos-fb => axis.0.motor-pos-fb
linksp Zpos-fb => axis.2.motor-pos-fb

linksp Xvel-cmd <= pid.0.output
linksp Zvel-cmd <= pid.1.output

linksp Xvel-cmd => pluto-servo.pwm.0.value
linksp Zvel-cmd => pluto-servo.pwm.1.value

setp pluto-servo.pwm.0.scale 26 # volts supply
setp pluto-servo.pwm.1.scale 26

setp pid.0.maxoutput 25 # volts at motor
setp pid.1.maxoutput 25

# the values below come from the ini
setp pid.0.Pgain [AXIS_0]P
setp pid.0.Igain [AXIS_0]I
setp pid.0.Dgain [AXIS_0]D
setp pid.0.bias [AXIS_0]BIAS
setp pid.0.FF0 [AXIS_0]FF0
setp pid.0.FF1 [AXIS_0]FF1
setp pid.0.FF2 [AXIS_0]FF2
setp pid.0.deadband [AXIS_0]DEADBAND

setp pid.1.Pgain [AXIS_2]P
setp pid.1.Igain [AXIS_2]I
setp pid.1.Dgain [AXIS_2]D
setp pid.1.bias [AXIS_2]BIAS
setp pid.1.FF0 [AXIS_2]FF0
setp pid.1.FF1 [AXIS_2]FF1
setp pid.1.FF2 [AXIS_2]FF2
setp pid.1.deadband [AXIS_2]DEADBAND

newsig Xpos-cmd float
newsig Zpos-cmd float
linksp Xpos-cmd <= axis.0.motor-pos-cmd
linksp Zpos-cmd <= axis.2.motor-pos-cmd
linkps pid.0.command <= Xpos-cmd
linkps pid.1.command <= Zpos-cmd

newsig Xenable bit
newsig Zenable bit
linkps axis.0.amp-enable-out => Xenable
linkps axis.2.amp-enable-out => Zenable

linksp Xenable => pid.0.enable
linksp Xenable => pluto-servo.pwm-0-enable
linksp Zenable => pid.1.enable
linksp Zenable => pluto-servo.pwm-1-enable

net estop-loop iocontrol.0.user-enable-out iocontrol.0.emc-enable-in

newsig Xvel float
newsig Xacc float
linksp Xpos-cmd => ddt.0.in
linkps ddt.0.out => Xvel
linksp Xvel => ddt.1.in
linkps ddt.1.out => Xacc
newsig Zvel float
newsig Zacc float
linksp Zpos-cmd => ddt.2.in
linkps ddt.2.out => Zvel
linksp Zvel => ddt.3.in
linkps ddt.3.out => Zacc

net tool-prep-loop iocontrol.0.tool-prepare iocontrol.0.tool-prepared
net tool-change-loop iocontrol.0.tool-change iocontrol.0.tool-changed

newsig Xhome bit
linkps pluto-servo.din.00-not => Xhome
linkps axis.0.home-sw-in <= Xhome
linkps axis.0.pos-lim-sw-in <= Xhome

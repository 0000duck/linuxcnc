#!@TCLSH@

# linuxcnc script exports LINUXCNC_TCL_DIR
# rip-environment exports TCLLIBPATH
# a RIP build desktop shortcut will not have TCLLIBPATH
if {    [info exists ::env(LINUXCNC_TCL_DIR)]
    && ![info exists ::env(TCLLIBPATH)] } {
  # prepend:
  set ::auto_path [lreplace $::auto_path -1 -1 $::env(LINUXCNC_TCL_DIR)]
}

if [catch {package require Linuxcnc} msg] {
  puts "haltcl config problem:$msg"
  catch {puts "LINUXCNC_TCL_DIR = $::env(LINUXCNC_TCL_DIR)"}
  catch {puts "TCLLIBPATH       = $::env(TCLLIBPATH)"}
  exit 1
}

if {   [llength $argv] > 0 \
    && (  ([lindex $argv 0] == "-i") || ([lindex $argv 0] == "-ini")  )} {
    parse_ini [lindex $argv 1]
    set argv [lrange $argv 2 end]
}

    
proc setp {p v} {
    set v [uplevel [list expr $v]]
    hal setp $p $v
}

proc sets {p v} {
    set v [uplevel [list expr $v]]
    hal sets $p $v
}

foreach c [hal --commands] {
    if {[info commands $c] == {}} {
        proc $c args "eval hal $c \$args"
    }
}

if {[llength $argv] == 0} {
    package require tclreadline
    namespace eval tclreadline {
      proc prompt1 {} {
        return "haltcl: "
      }
    }
    ::tclreadline::Loop
}

set filename [lindex $argv 0]
set argv [lrange $argv 1 end]

set result [source $filename]

